
buildscript {
    ext {
        minSdkVersion = 19
        targetSdkVersion = 23
        compileSdkVersion = 29
        buildToolsVersion = '29.0.2'
        javaVersion = JavaVersion.VERSION_1_8

        MATRIX_VERSION = "2.0.5-SNAPSHOT"

        GROUP = 'com.tencent.matrix'
        VERSION_NAME = "${MATRIX_VERSION}"

        /**
         * To compile Sample App with matrix source code for further debug:
         *
         * 1. Run `gradle publishToMavenLocal`
         * 2. Set 'compileMatrixWithSrc = true' or with run gradle with args '-PcompileWithSrc=true'
         *
         # Or see `testBuildAndroid` in `.circleci/config.yaml`.
         */
        compileMatrixWithSrc = false

        useLocalMaven = {
            return compileMatrixWithSrc ?: properties?.compileWithSrc ?: false
        }

        if (useLocalMaven()) {
            if (!MATRIX_VERSION.endsWith("-SNAPSHOT")) {
                MATRIX_VERSION = "${MATRIX_VERSION}-SNAPSHOT"
            }
        }
    }

    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath 'com.google.code.gson:gson:2.7'
        classpath  'com.google.gradle:osdetector-gradle-plugin:1.6.0'
        classpath ("com.tencent.matrix:matrix-gradle-plugin:${MATRIX_VERSION}") {
            changing = true
            exclude group: 'com.android.tools.build', module: 'gradle'
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}

